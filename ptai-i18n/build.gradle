buildscript {
    repositories {
        mavenCentral()
        maven { url = uri('https://repo.jenkins-ci.org/releases/') }
    }
    dependencies {
        classpath("org.jvnet.localizer:maven-localizer-plugin:${localizerVersion}")
    }
}

plugins {
    id 'java-library'
    id 'java-test-fixtures'
}

group = "${rootGroup}"

dependencies {
    // https://mvnrepository.com/artifact/org.jvnet.localizer/localizer
    implementation "org.jvnet.localizer:localizer:${localizerVersion}"
    // Need this to write tests that use some common functions like
    // logging, zipped data processing, JSON parse etc.
    testFixturesApi(testFixtures(project(":pt-misc-tools")))
}

import org.jvnet.localizer.GeneratorTask

tasks.register('generate-sources') {
    doLast {
        def sourceDirs = sourceSets.main.resources.srcDirs
        sourceDirs.findAll { it.exists() }.each { File sourceDirectory ->
            GeneratorTask generator = new GeneratorTask()
            generator.project = ant.project
            generator.dir = sourceDirectory
            generator.todir = project.file("$buildDir/generated")
            generator.includes = '**/Resources.properties'
            generator.execute()
        }
    }
}

compileJava.dependsOn tasks.getByPath("generate-sources")
sourceSets.main.java.srcDirs += ["$buildDir/generated"]
