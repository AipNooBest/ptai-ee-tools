plugins {
    id 'io.freefair.lombok' version '6.1.0' apply false
    id 'com.github.johnrengelman.shadow' version '5.2.0' apply false
    id 'org.jenkins-ci.jpi' version '0.49.0' apply false
    id 'com.palantir.git-version' version '0.12.3' apply false
    id 'org.cyclonedx.bom' version '1.7.4' apply false
}

allprojects {
    apply plugin: 'com.palantir.git-version'
    ext {
        buildTimestamp = "${Instant.now().toString()}"
        versionDetails = versionDetails()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'maven-publish'

    apply plugin:org.cyclonedx.gradle.CycloneDxPlugin
    cyclonedxBom {
        projectType = "library"
        destination = file("build/reports")
        outputName = "bom"
        outputFormat = "json"
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    group = "${rootGroup}"

    repositories {
        mavenLocal()
        maven { url mavenCentralRepoUrl }
        maven { url gradlePluginRepoUrl }
        maven { url jenkinsReleasesRepoUrl }
        maven { url teamcityRepoUrl }
    }

    dependencies {
        // https://mvnrepository.com/artifact/com.fasterxml.jackson.dataformat/jackson-dataformat-xml
        implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-xml:${jacksonVersion}"
        // Enable Slf4J log facade
        implementation "org.slf4j:slf4j-api:$slf4jVersion"

        // Use JUnit 5 for tests
        testImplementation(platform("org.junit:junit-bom:$junitVersion"))
        testImplementation('org.junit.jupiter:junit-jupiter')
        // Enable Slf4J log for tests
        testImplementation "org.slf4j:slf4j-api:$slf4jVersion"
        // ... and use Log4J2 provider for them
        // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-api
        testRuntimeOnly "org.apache.logging.log4j:log4j-api:${log4jVersion}"
        // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
        testRuntimeOnly "org.apache.logging.log4j:log4j-core:${log4jVersion}"
        // https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-slf4j-impl
        testRuntimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        archiveClassifier = 'javadoc'
        from javadoc.destinationDir
    }

    task testsJar(type: Jar, dependsOn: testClasses) {
        archiveClassifier = 'tests'
        from sourceSets.test.output
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked"
    }

    tasks.withType(Javadoc) {
        options.encoding = 'UTF-8'
        // OpenAPI-generated files contain some javadoc issues, let's ignore them
        // Also delomboked files are also generate warnings
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    jar {
        manifest.attributes['Implementation-Version'] = project.version
        manifest.attributes['Build-Time'] = buildTimestamp
        manifest.attributes['Implementation-Vendor-Id'] = rootGroup
        manifest.attributes['Implementation-Git-Hash'] = versionDetails.gitHash
        manifest.attributes['Implementation-Git-Hash-Full'] = versionDetails.gitHashFull
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
                artifact testsJar

                suppressPomMetadataWarningsFor('testFixturesApiElements')
                suppressPomMetadataWarningsFor('testFixturesRuntimeElements')

                pom {
                    name = project.name
                    packaging = 'jar'
                    inceptionYear = '2018'
                    url = 'https://www.ptsecurity.com/ww-en/products/ai/'
                    description = project.description

                    scm {
                        url = 'https://github.com/ZhukovAN/ptaiPlugins'
                        connection = 'scm:git:https://github.com/ZhukovAN/ptaiPlugins.git'
                        developerConnection = 'scm:git:https://github.com/ZhukovAN/ptaiPlugins.git'
                    }

                    developers {
                        developer {
                            id = 'ZhukovAN'
                            name = 'Alexey Zhukov'
                            email = 'alexey.n.zhukov@gmail.com'
                        }
                    }
                }
            }
        }
        repositories {
            mavenLocal()
        }
    }

    test {
        useJUnitPlatform() {
            excludeTags 'integration', 'development'
        }
    }

    task integrationScanTest(type: Test) {
        useJUnitPlatform {
            includeTags 'integration & scan'
            excludeTags 'slow', 'jenkins'
        }
        shouldRunAfter test
    }

    task integrationFastTest(type: Test) {
        useJUnitPlatform {
            includeTags 'integration'
            excludeTags 'slow', 'scan', 'jenkins'
        }
        shouldRunAfter = [ "test" ]
    }

    task integrationJenkinsTest(type: Test) {
        useJUnitPlatform {
            includeTags 'jenkins'
        }
        shouldRunAfter = [ "test" ]
    }

    task integrationSlowTest(type: Test) {
        useJUnitPlatform {
            includeTags 'slow'
        }
        shouldRunAfter = [ "test" ]
    }
}

wrapper {
    gradleVersion = '8.4'
}
